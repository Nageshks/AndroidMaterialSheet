// Strategy for reading property
// 1.Read from both environment and local file
// 2.Read Local file should override System Environment
// 3.Support to define half property from System and local file
ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["sonatypeStagingProfileId"] = ''

// Read all local properties
File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(secretPropsFile).withCloseable { is -> p.load(is) }
    ext["ossrhUsername"] = p.getOrDefault("ossrhUsername", System.getenv("OSSRH_USERNAME"))
    ext["ossrhPassword"] = p.getOrDefault("ossrhPassword", System.getenv("OSSRH_PASSWORD"))
    ext["sonatypeStagingProfileId"] = p.getOrDefault("sonatypeStagingProfileId", System.getenv("SONATYPE_STAGING_PROFILE_ID"))
    ext["signing.keyId"] = p.getOrDefault("signing.keyId", System.getenv("SIGNING_KEY_ID"))
    ext["signing.password"] = p.getOrDefault("signing.password", System.getenv("SIGNING_PASSWORD"))
    ext["signing.secretKeyRingFile"] = p.getOrDefault("signing.secretKeyRingFile", System.getenv("SIGNING_SECRET_KEY_RING_FILE"))
} else {
    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
    ext["sonatypeStagingProfileId"] = System.getenv('SONATYPE_STAGING_PROFILE_ID')
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
}

// Set up Sonatype repository
nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            stagingProfileId = sonatypeStagingProfileId
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}
